// the image can be part of a slider
prototype(MyVendor.AwesomeNeosProject:Content.Image) < prototype(MyVendor.AwesomeNeosProject:Content.AbstractSlide) {
    slideContent = MyVendor.AwesomeNeosProject:Content.BasicImage
}

// use basic image when including in other components
prototype(MyVendor.AwesomeNeosProject:Content.BasicImage) < prototype(Neos.Fusion:Value) {

    @context._textAlignClass = MyVendor.AwesomeNeosProject:Utility.TextAlign {
        textAlign = ${q(node).property('textAlign')}
    }

    value = MyVendor.AwesomeNeosProject:Utility.ContentWidth {
        class = ${AtomicFusion.classNames(_textAlignClass, 'image__wrapper')}
        size = ${q(node).property('contentWidth')}

        content = MyVendor.AwesomeNeosProject:Component.Image {
            imageSource = Sitegeist.Kaleidoscope:AssetImageSource {
                asset = ${q(node).property('image')}
            }
            imageUri = Neos.Fusion:ResourceUri {
                resource = ${q(node).property('image').resource}
            }
            fileExtension = ${q(node).property('image').resource.fileExtension}
            alternativeText = MyVendor.AwesomeNeosProject:Helper.ImageAltText {
                image = ${q(node).property('image')}
            }
            imageDescription = ${q(node).property('imageDescription')}

            href = ${q(node).property('link')}
            href.@process.convert = Neos.Neos:ConvertUris
            isExternalLink = ${q(node).property('isExternalLink')}
            contentWidth = ${q(node).property('contentWidth')}
        }
    }
}
