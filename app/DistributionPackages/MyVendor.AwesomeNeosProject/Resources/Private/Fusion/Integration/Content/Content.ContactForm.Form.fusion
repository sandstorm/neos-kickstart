prototype(MyVendor.AwesomeNeosProject:Content.ContactForm.Form) < prototype(Neos.Fusion.Form:Runtime.RuntimeForm) {

    namespace = "contactform"
    attributes.id = "contact-form"
    attributes.class = "form"

    process {
        content = MyVendor.AwesomeNeosProject:Component.ContactForm

        schema {
            # surname = honeypot
            surname = ${Form.Schema.string()}
            name = ${Form.Schema.string().isRequired()}
            email = ${Form.Schema.string().isRequired().validator('Neos\Flow\Validation\Validator\EmailAddressValidator')}
            message = ${Form.Schema.string()}
            selection = ${Form.Schema.string()}

            attachment1 = ${Form.Schema.resource().validator('Neos\Fusion\Form\Runtime\Validation\Validator\FileTypeValidator', {allowedExtensions:['pdf']})}
        }
    }

    action {
        message {
            type = 'Neos.Fusion.Form.Runtime:Message'
            options {
                message = MyVendor.AwesomeNeosProject:Component.SentMessage
            }
        }
        email {
            # email is not send when honey pot field is filled out
            @if.noHoney = ${data.surname ? false : true}
            type = 'Neos.Fusion.Form.Runtime:Email'
            options {
                senderAddress = "contact@sandstorm.de"
                recipientAddress = ${q(node).property('mailTo')}
                subject = ${'Contact form from: ' + q(documentNode).property('title')}
                text = MyVendor.AwesomeNeosProject:Component.MailBodyText
                html = MyVendor.AwesomeNeosProject:Component.MailBodyHtml
                attachments {
                    attachment1 = ${data.attachment1}
                }
            }
        }
    }
}
