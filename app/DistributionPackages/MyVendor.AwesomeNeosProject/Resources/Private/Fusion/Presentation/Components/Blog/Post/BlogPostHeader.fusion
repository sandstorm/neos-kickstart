prototype(MyVendor.AwesomeNeosProject:Component.BlogPostHeader) < prototype(Neos.Fusion:Component) {
    title = ''
    teaserText = ''
    authors = ''
    tags = ''
    date = ''
    image = ''

    @propTypes {
        @strict = true
        title = ${PropTypes.string}
        teaserText = ${PropTypes.string}
        authors = ${PropTypes.string}
        tags = ${PropTypes.string}
        date = ${PropTypes.any}
        image = ${PropTypes.string}
    }

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        renderer = afx`
            <MyVendor.AwesomeNeosProject:Utility.ContentWidth>
                <header class="blog-post-header">
                    <h1>{props.title}</h1>
                    <div class="blog-post-header__meta_data">
                        <span @if.hasAuthor={props.authors} class="blog-post-header__authors">
                            {props.authors}
                        </span>
                        <span class="blog-post-header__date">{Date.format(props.date, 'd.m.Y')}</span>
                        {props.tags}
                    </div>
                    <div class="blog-post-header__image">
                        {props.image}
                    </div>
                    <div @if.hasTeaserText={props.teaserText} class="blog-post-header__teaser">{props.teaserText}</div>
                </header>
            </MyVendor.AwesomeNeosProject:Utility.ContentWidth>
        `
    }
}
