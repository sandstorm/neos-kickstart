prototype(MyVendor.AwesomeNeosProject:Component.PageMenu.Main) < prototype(Neos.Fusion:Component) {
    menuItems = Neos.Neos:MenuItems {
        maximumLevels = 2
        filter = "
            Neos.Neos:Document,
            !MyVendor.AwesomeNeosProject:Document.NotFoundPage,
            !MyVendor.AwesomeNeosProject:Document.Blog.Tags,
            !MyVendor.AwesomeNeosProject:Document.Blog.Post
        "
    }

    renderer = afx`
        <div class="page-menu__main">
            <ul class="page-menu__toplevel" x-data="{ openListItem: '' }">
                <Neos.Fusion:Loop items={props.menuItems}>
                    <li
                        class={AtomicFusion.classNames(item.state, item.subItems ? 'page-menu__toplevel-item--has-items' : '' , 'page-menu__toplevel-item')}
                        x-id="['menu-list-item']"
                    >
                        <Neos.Neos:NodeLink node={item.node} attributes.:list-id="$id('menu-list-item')" attributes.x-on:focus="openListItem = $event.target.getAttribute('list-id')">
                            {item.label}
                        </Neos.Neos:NodeLink>

                        <div @if.hasItems={item.subItems} :list-id="$id('menu-list-item')" class="page-menu__sublevel-wrapper" :class="openListItem == $id('menu-list-item') ? 'page-menu__sublevel-wrapper--is-visible' : ''">
                            <MyVendor.AwesomeNeosProject:Utility.ContentWidth>
                                <ul
                                    @if.has={item.subItems}
                                    class="page-menu__sublevel"
                                >
                                    <Neos.Fusion:Loop items={item.subItems} itemName="item">
                                        <li class={AtomicFusion.classNames(item.state, 'page-menu__sublevel-item')}>
                                            <Neos.Neos:NodeLink node={item.node}>
                                                {item.label}
                                            </Neos.Neos:NodeLink>
                                        </li>
                                    </Neos.Fusion:Loop>
                                </ul>
                            </MyVendor.AwesomeNeosProject:Utility.ContentWidth>
                        </div>
                    </li>
                </Neos.Fusion:Loop>
            </ul>
        </div>
    `
}
