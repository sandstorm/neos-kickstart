prototype(MyVendor.AwesomeNeosProject:Component.PageMenu.Main) < prototype(Neos.Fusion:Component) {
    menuItems = Neos.Neos:MenuItems {
        maximumLevels = 2
        filter = "
            Neos.Neos:Document,
            !MyVendor.AwesomeNeosProject:Document.NotFoundPage,
            !MyVendor.AwesomeNeosProject:Document.Blog.Tags,
            !MyVendor.AwesomeNeosProject:Document.Blog.Post
        "
    }

    // we use alpine js to toggle the main menu for mobile screen size
    // :class sets the visible class depending on the 'openMobileMenu' state (See PageMenu.fusion)
    renderer = afx`
        <div class="page-menu__main" :class="openMobileMenu ? 'page-menu__main--is-visible' : ''">
            <ul class="page-menu__toplevel">
                <Neos.Fusion:Loop items={props.menuItems}>
                    <li
                        class={AtomicFusion.classNames(item.state, 'page-menu__toplevel-item', {'page-menu__toplevel-item--has-items': item.subItems})}
                    >
                        <Neos.Neos:NodeLink
                            node={item.node}
                            attributes.title={I18n.translate('MyVendor.AwesomeNeosProject:Base:linkToPage') + item.label}
                        >
                            {item.label}
                        </Neos.Neos:NodeLink>

                        <div @if.hasItems={item.subItems} class="page-menu__sublevel-wrapper">
                            <MyVendor.AwesomeNeosProject:Utility.ContentWidth size="small">
                                <ul
                                    @if.has={item.subItems}
                                    class="page-menu__sublevel"
                                >
                                    <Neos.Fusion:Loop items={item.subItems} itemName="item">
                                        <li class={AtomicFusion.classNames(item.state, 'page-menu__sublevel-item')}>
                                            <Neos.Neos:NodeLink
                                                node={item.node}
                                                attributes.title={I18n.translate('MyVendor.AwesomeNeosProject:Base:linkToPage') + item.label}
                                            >
                                                {item.label}
                                            </Neos.Neos:NodeLink>
                                        </li>
                                    </Neos.Fusion:Loop>
                                </ul>
                            </MyVendor.AwesomeNeosProject:Utility.ContentWidth>
                        </div>
                    </li>
                </Neos.Fusion:Loop>
            </ul>
        </div>
    `
}
