prototype(MyVendor.AwesomeNeosProject:Component.Map) < prototype(Neos.Fusion:Component) {
    lat = 0
    lat.@process.toFloat = ${String.toFloat(value)}
    lon = 0
    lon.@process.toFloat = ${String.toFloat(value)}
    zoom = 14
    zoom.@process.toFloat = ${String.toFloat(value)}

    popupText = ''

    @propTypes {
        @strict = true
        lat = ${PropTypes.float.isRequired}
        lon = ${PropTypes.float.isRequired}
        zoom = ${PropTypes.float.isRequired}
        popupText = ${PropTypes.string}
    }

    renderer = Neos.Fusion:Component {
        mapInitializer = Neos.Fusion:DataStructure {
            lat = ${props.lat}
            lon = ${props.lon}
            zoom = ${props.zoom}
            popupText = ${props.popupText}
        }

        renderer = afx`
            <link rel="stylesheet" href="/_maptiles/frontend/v1/map-main.css" />
            <div class="content-width" style="height: 100vh" x-data={'map(' + Json.stringify(props.mapInitializer) + ')'} x-intersect.once="loadMap()"></div>
        `
    }
}
