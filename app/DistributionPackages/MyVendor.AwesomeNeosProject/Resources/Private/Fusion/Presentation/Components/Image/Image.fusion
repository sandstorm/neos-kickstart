prototype(MyVendor.AwesomeNeosProject:Component.Image) < prototype(Neos.Fusion:Component) {
    alternativeText = ''
    description = ''
    imageSource = null
    href = ''
    isExternalLink = false

    inBackend = ${node.context.inBackend}

    srcSet = ${['320w', '600w', '800w', '1200w']}

    sizes = ${[
    '(min-width: 1400px) 1200px',
    '(min-width: 996px) 996px',
    '(min-width: 768px) 768px',
    '(min-width: 500px) 500px',
    '(min-width: 374px) 374px',
    '100vw'
    ]}

    formats = ${['webp', 'png']}

    @propTypes {
        @strict = true
        imageSource = ${PropTypes.any}
        alternativeText = ${PropTypes.string}
        description = ${PropTypes.string}
        contentWidth = ${PropTypes.string}
        textAlign = ${PropTypes.string}
        href = ${PropTypes.string}
        isExternalLink = ${PropTypes.boolean}

        inBackend = ${PropTypes.boolean}

        srcSet = ${PropTypes.arrayOf(PropTypes.string)}
        sizes = ${PropTypes.arrayOf(PropTypes.string)}
        formats = ${PropTypes.arrayOf(PropTypes.string)}
    }

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        renderer = Neos.Fusion:Tag {
            tagName = ${(props.href && !props.inBackend) ? 'a' : 'div'}
            attributes.class = ${AtomicFusion.classNames('image')}
            attributes.href = ${props.href ? props.href : null}
            attributes.target = ${props.isExternalLink ? '_blank' : null}

            content = afx`
                <MyVendor.AwesomeNeosProject:Component.ImagePlaceholder @if.showPlaceholder={props.inBackend && !props.imageSource}/>
                <Sitegeist.Kaleidoscope:Picture
                    alt={props.alternativeText}
                    imageSource={props.imageSource}
                    srcset={props.srcSet}
                    sizes={props.sizes}
                    formats={props.formats}
                    renderDimensionAttributes={false}
                    @if.showImage={props.imageSource}
                />
                <span @if.description={props.description} class="image__description">{props.description}</span>
            `
        }
    }
}
