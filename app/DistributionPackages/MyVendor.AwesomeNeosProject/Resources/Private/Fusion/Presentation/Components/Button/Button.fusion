prototype(MyVendor.AwesomeNeosProject:Component.Button) < prototype(Neos.Fusion:Component) {
    text = ''
    href = ''
    isExternalLink = false
    type = ''
    align = ''

    @propTypes {
        @strict = true
        text = ${PropTypes.string}
        href = ${PropTypes.string}
        isExternalLink = ${PropTypes.boolean}
        type = ${PropTypes.oneOf(['solid-blue', 'solid-grey', 'outline', 'link'])}
        align = ${PropTypes.oneOf(['left', 'center', 'right'])}
    }

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}

        _inBackend = MyVendor.AwesomeNeosProject:Helper.InBackend

        renderer = afx`
            <MyVendor.AwesomeNeosProject:Utility.ContentWidth
                class={AtomicFusion.classNames(
                    'button__wrapper',
                    'button__wrapper--' + props.align
                )}
            >
                <Neos.Fusion:Tag
                    tagName={(props.href && !props._inBackend) ? 'a' : 'div'}
                    attributes.class={AtomicFusion.classNames(
                        'button',
                        'button--' + props.type
                    )}
                    attributes.href={props.href ? props.href : null}
                    attributes.target={props.isExternalLink ? '_blank' : null}
                >
                    {props.text}
                </Neos.Fusion:Tag>
            </MyVendor.AwesomeNeosProject:Utility.ContentWidth>
        `
    }
}
