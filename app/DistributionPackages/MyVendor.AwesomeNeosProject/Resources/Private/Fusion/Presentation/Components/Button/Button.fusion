prototype(MyVendor.AwesomeNeosProject:Component.Button) < prototype(Neos.Fusion:Component) {
    text = ''
    href = ''
    isExternalLink = false
    inBackend = ${node.context.inBackend}
    type = ''

    @propTypes {
        @strict = true
        text = ${PropTypes.string}
        href = ${PropTypes.string}
        type = ${PropTypes.oneOf(['primary', 'secondary'])}
        isExternalLink = ${PropTypes.boolean}
        inBackend = ${PropTypes.boolean}
    }

    renderer = Neos.Fusion:Component {
        @apply.props = ${props}
        _target = ${props.isExternalLink ? '_blank' : null}
        _buttonClassNames = Neos.Fusion:Case {
            primary {
                condition = ${props.type == 'primary'}
                renderer = 'button--primary'
            }
            secondary {
                condition = ${props.type == 'secondary'}
                renderer = 'button--seondary'
            }
            default {
                condition = ${true}
                renderer = ''
            }
        }
        renderer =  afx`
            <MyVendor.AwesomeNeosProject:Utility.ContentWidth class="button__outer">
                <Neos.Fusion:Tag
                    tagName={(props.href && !props.inBackend) ? 'a' : 'div'}
                    attributes.class={AtomicFusion.classNames('button' , props._buttonClassNames)}
                    attributes.href={props.href ? props.href : null}
                    attributes.target={props.isExternalLink ? props._target : null}
                    attributes.data-controller={'ScrollTransition'}
                >
                    {props.text}
                </Neos.Fusion:Tag>
            </MyVendor.AwesomeNeosProject:Utility.ContentWidth>
        `
    }
}
