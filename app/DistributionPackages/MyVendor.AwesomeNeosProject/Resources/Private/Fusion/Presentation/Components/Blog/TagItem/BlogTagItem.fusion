prototype(MyVendor.AwesomeNeosProject:Component.BlogTagItem) < prototype(Neos.Fusion:Component) {
    tag = null
    activeTag = null
    renderAsLink = true

    @propTypes {
        tag = ${PropTypes.any}
        activeTag = ${PropTypes.any}
        renderAsLink = ${PropTypes.boolean}
    }

    @context.node = ${this.tag}
    tagPage = Neos.Neos:NodeUri {
        node = ${node}
    }

    renderer = Neos.Fusion:Tag {
        tagName = ${props.renderAsLink ? 'a' : 'span'}
        attributes.class = ${AtomicFusion.classNames('blog-tag', {'blog-tag--is-active': props.activeTag == props.tag})}
        attributes.href = ${props.renderAsLink ? props.tagPage : null}
        attributes.title = ${props.renderAsLink ? I18n.translate('MyVendor.AwesomeNeosProject:Base:linkTo') + ' Tag: ' + props.tag.properties.title : null}

        content = afx`
            {props.tag.properties.title}
        `
    }
}
